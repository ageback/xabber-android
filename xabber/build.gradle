apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'
apply plugin: 'com.frogermcs.androiddevmetrics'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 327
        versionName '1.0.155'
    }

    lintOptions {
        // problems with generated "applicationId" string resource
        disable 'MissingTranslation'
        disable 'InvalidPackage'

        abortOnError false
    }

    subprojects {
        afterEvaluate {
            if (getPlugins().hasPlugin('android') ||
                    getPlugins().hasPlugin('android-library')) {

                configure(android.lintOptions) {
                    abortOnError false

                }
            }
        }
    }

    productFlavors {

        dev {
            applicationId "com.xabber.androiddev"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        beta {
            applicationId "com.xabber.android.beta"
            resValue 'string', 'application_package', applicationId
            multiDexEnabled true
        }

        prod {
            applicationId "com.xabber.android"
            resValue 'string', 'application_package', applicationId
        }

        vip {
            applicationId "com.xabber.androidvip"
            resValue 'string', 'application_package', applicationId
        }

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'x86', 'x86_64'

            universalApk true
        }
    }
}

ext {
    smackVersion = '4.2.0-rc3'
    supportVersion = '25.1.1'
}

dependencies {
    compile project('MemorizingTrustManager')
    compile project('emojicon')

    compile "org.igniterealtime.smack:smack-android-extensions:$smackVersion"
    compile "org.igniterealtime.smack:smack-experimental:$smackVersion"
    compile "org.igniterealtime.smack:smack-tcp:$smackVersion"

    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:support-v13:$supportVersion"

    compile 'org.jitsi:org.otr4j:0.22'
    compile 'com.google.zxing:android-integration:3.3.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'org.greenrobot:eventbus:3.0.0'

    compile 'com.soundcloud.android:android-crop:1.0.1@aar'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
}

configurations {
    all*.exclude group: 'xpp3', module: 'xpp3'
}
